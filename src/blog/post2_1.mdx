## Tailwind

While writing React apps, I grew accustomed to using
[TailwindCSS](https://tailwindcss.com/) for styling individual elements.
Tailwind defines many short class names for commonly used CSS. For example,
`class="py-4"` in HTML will correspond to `.py-4 { padding-top: 1rem; padding-bottom: 1rem; }` in CSS.

To minimize the size of Tailwind's CSS file, it scans your source code to see which classes you
are using. This is normally done on javascript or typescript, but Tailwind can
be configured to run on any file, including rust:

```js
// /tailwind.config.js
module.exports = {
  content: ["src/**/*.rs"],
  // ...
};
```

With Tailwind configured, we need a way for bazel to run the Tailwind cli,
`tailwindcss`, and generate our CSS file. Instead of relying on the developer to
have `tailwindcss` installed, we can do things the Bazel way and have a specific
version of the Tailwind toolchain installed.

A `package.json` file defines what libraries we need

```json
//  /package.json
{
  "name": "implfuture",
  "version": "0.1.0",
  "dependencies": {},
  "devDependencies": {
    "tailwindcss": "^3.1.8"
  }
}
```

And in our `WORKSPACE` file, we install those packages

```py
# /WORKSPACE
# use bazel rules_nodesjs to install
yarn_install(
    name = "root_npm",
    package_json = "//:package.json",
    yarn_lock = "//:yarn.lock",
)
```

The `tailwindcss` binary is now available to other bazel rules we write. Bazel
has a special rule called
[`genrule`](https://bazel.build/reference/be/general#genrule) that lets you
define a rule using a shell command. Here's the `genrule` to generate the
tailwind CSS file.

```py
# /BUILD

genrule(
    name = "tailwind",
    # include our rust files to scan for used class names, as well as the
    # tailwind configuration
    srcs = glob(["src/**/*.rs"]) + ["tailwind.config.js"],
    outs = ["static/tailwind.css"],
    cmd = "$(execpath @root_npm//tailwindcss/bin:tailwindcss) --output=$(OUTS)",
    # pull in npm dependency
    tools = ["@root_npm//tailwindcss/bin:tailwindcss"],
    visibility = ["//:__pkg__"],
)

```

## PrismJS

No good developer blog post is complete without syntax highlighting. I tried to
find a rust-native implementation of syntax highlighting, but I couldn't find
anything as full-featured as PrismJS (which is what I used for the previous
rewrite of this site). While I had avoided javascript for as long as possible, I
thought this might be a good opportunity to try incorporating a JS library with
a Yew app.

My approach was to create a separate Javascript file that would bundle any JS
dependencies our app uses and link to it in the `index.html` file. In the
interest of keeping things bazel-y, I defined another `package.json` for runtime
dependencies in the `bundle/` folder:

```json
// /bundle/package.json

{
  "name": "implfuture",
  "version": "0.1.0",
  "dependencies": {
    "prismjs": "^1.28.0"
  }
}
```

```py
# /WORKSPACE

yarn_install(
    name = "app_npm",
    package_json = "//bundle:package.json",
    yarn_lock = "//bundle:yarn.lock",
)
```

Since there are bazel rules for it, I used the [`esbuild`](https://esbuild.github.io/) bundler to bundle
prism. There's even an [esbuild prismjs
plugin](https://npm.io/package/esbuild-plugin-prismjs) that helps configure
PrismJS features and included languages.

The whole app bundle is pulled together in the `bundle/BUILD` file:

```py
# /bundle/BUILD

# typescript bundle entrypoint
ts_project(
    name = "tsproject",
    srcs = [
        "app.ts",
    ],
    deps = ["@app_npm//prismjs"],
)

# output bundle, served by web server
esbuild(
    name = "bundle",
    config = ":esbuild_config",
    entry_point = "app.ts",
    visibility = ["//:__pkg__"],
    deps = [
        ":tsproject",
    ],
)

esbuild_config(
    name = "esbuild_config",
    # includes configuration of PrismJS
    config_file = "esbuild.config.mjs",
    deps = [
        "@root_npm//esbuild",
        "@root_npm//esbuild-plugin-prismjs",
    ],
)
```

With PrismJS available to my app at runtime, I could now use it from Yew.
PrismJS can be configured to automatically syntax highlight any code it finds in
the DOM, but care must be taken when using Yew. Yew expects to be the only code
manipulating the DOM. If something else adds or removes DOM nodes, it can cause
unexpected behavior when Yew comes back to re-render that part of the page.

To have full control over when Prism highlights a code block, I chose to use the
`Prism.highlightElement` function. First, it must be defined in rust as `extern fn`:

```rust
mod prism {
    use wasm_bindgen::prelude::*;
    #[wasm_bindgen]
    extern "C" {
        #[wasm_bindgen(js_namespace = Prism)]
        pub fn highlightElement(element: web_sys::Element);
    }
}
```

Then I defined a Yew component that highlights code:

```rust
fn HighlightCode(c: &super::ChildProps) -> Html {
    //  save code tag to be used for highlighting
    let code_ref = use_state_eq(|| NodeRef::default());
    let mut code_tag = c.children.iter().next().unwrap().clone();
    match &mut code_tag {
        VNode::VTag(t) => t.node_ref = (*code_ref).clone(),
        _ => {}
    };

    use_effect_with_deps(
        move |_| {
            //  highlight code whenever children change, causes Prism to add DOM
            //  nodes under .codecontainer div
            let element = code_ref.cast::<Element>().unwrap();
            prism::highlightElement(element.clone());

            //  cleanup: remove DOM nodes created by Prism
            move || {
                element
                    .closest(".codecontainer")
                    .ok()
                    .flatten()
                    .map(|e| e.remove());
            }
        },
        c.children.clone(),
    );

    html! {
        //  wrap everything in a .codecontainer div for easier cleanup
        <div class="codecontainer">
            <pre class="overflow-auto m-4 p-6 bg-gray-300/5 rounded">
                {code_tag}
            </pre>
        </div>
    }
}
```

This component makes use of
[`use_effect_with_deps`](https://docs.rs/yew/latest/yew/functional/fn.use_effect_with_deps.html)
to re-highlight code whenever the contents of the DOM node change. The closure
returned in `use_effect_with_deps` is the cleanup code, and is run whenever this
component is un-mounted from the DOM. The cleanup code is meant to restore the
DOM to its previous state before the Prism highlighting. It works in practice,
but I still get a warning from Yew (`app_wasm.js:437 Node not found to remove VTag`)
when I navigate away from a page with syntax highlighting. Clearly this still
needs some tweaking ðŸ™‚.

## WASM Code-Size Optimization

One obstacle with compiling rust to wasm is that the resulting wasm file can
be pretty large. As of writing this blog, the unoptimized wasm for this site is
5.1MB. This is clearly way too big for a website. Even though SSR will give the
user a contentful page immediately, all interactivity like `onclick`s and
`use_effect`s is delayed until the app wasm fully loads and hydration runs.

The rustwasm book has some great tips on optimizing [wasm code
size](https://rustwasm.github.io/docs/book/game-of-life/code-size.html).

### wasm-opt

`wasm-opt` is a part of the Emscripten SDK and is included in its [bazel
toolchain](https://github.com/emscripten-core/emsdk/blob/26a0dea0d3bbf616fa7f0a908e5b08aab406f7c4/bazel/emscripten_deps.bzl).
`wasm-opt` optimizes wasm code for both performance and code-size. Using
`wasm-opt -Os` reduced the app wasm size from 4.9MB to 1.3MB.

Incorporating `wasm-opt` into bazel in a cross-platform way was a little tricky.
The `emsdk` bazel toolchain exposes `wasm-opt` at different paths [depending on
the
platform](https://github.com/emscripten-core/emsdk/blob/26a0dea0d3bbf616fa7f0a908e5b08aab406f7c4/bazel/emscripten_deps.bzl#L117).
This meant that a `genrule` would have to be aware of which platform it was
running on in order to locate the correct `wasm-opt`. Instead, I wrote a custom [rule](https://bazel.build/extending/rules) that invokes `wasm-opt` using starlark:

```py
# /emsdk/emsdk.bzl

# expects exactly one .wasm file and one output path
def _wasmopt_impl(ctx):
    tc = ctx.toolchains["//emsdk:toolchain_type"]
    info = tc.emsdkinfo
    wasm_srcs = [f for f in ctx.attr.src.files.to_list() if f.path.endswith(".wasm")]
    if len(wasm_srcs) != 1:
        fail("expected 1 wasm file, got %s" % wasm_srcs)
    wasm_src = wasm_srcs[0]

    ctx.actions.run(
        inputs = [wasm_src],
        outputs = [ctx.outputs.out],
        executable = info.wasmopt,
        arguments = ["-Os", wasm_src.path, "-o", ctx.outputs.out.path],
    )
```

This rule uses the bazel [toolchain](https://bazel.build/extending/toolchains)
construct to select the version of `emsdk` compatible with the host. I also
defined `emsdk` toolchains, similar to how `rust` or `go` toolchains are defined:

```py
# /emsdk/emsdk.bzl

def declare_toolchains(name):
    for tc in TOOLCHAINS:
        emsdk_toolchain(
            name = "emsdk_{tc}".format(tc = tc),
            linker_files = "@emscripten_bin_{tc}//:linker_files".format(tc = tc),
        )
        native.toolchain(
            name = "emsdk_{tc}_toolchain".format(tc = tc),
            toolchain = ":emsdk_{tc}".format(tc = tc),
            exec_compatible_with = TOOLCHAINS[tc]["exec"],
            toolchain_type = "//emsdk:toolchain_type",
        )

def register_toolchains():
    native.register_toolchains(
        *["//emsdk:emsdk_{tc}_toolchain".format(tc = tc) for tc in TOOLCHAINS]
    )
```

Any bazel project then just needs to call `register_toolchains()` in its
`WORKSPACE` file and the correct `emsdk` will be downloaded when the `wasmopt`
rule is used.

### Brotli

While `wasm-opt` brought the app down from 4.9MB to 1.3MB, that's still way too big.
The next option I looked at was compression. After experimenting
with flate, gzip, and brotli, I found that brotli achieved the smallest size.
With both `wasm-opt -Os` and `brotli -9`, the wasm file came down to 331KB in
total.

Incorporating [brotli](https://github.com/google/brotli) into bazel was pretty
simple since it's a Google project that already uses bazel for its build. First I added it to my `WORKSPACE`:

```py
# /WORKSPACE

git_repository(
    name = "brotli",
    commit = "9801a2c5d6c67c467ffad676ac301379bb877fc3",  # 2022-05-12
    remote = "https://github.com/google/brotli",
)
```

Then I wrote a simple `genrule` to compress the output of `wasm-opt`:

```py
# /BUILD

genrule(
    name = "app_wasm_opt_br",
    srcs = [":app_wasm_opt"],
    outs = ["app_wasm_bg_opt.wasm.br"],
    cmd = "$(execpath @brotli) -9 $<",
    tools = ["@brotli"],
)
```

Referencing `@brotli` in the `tools` argument automatically compiles `brotli`
for the host architecture. This way, I can be sure exactly which version of
`brotli` is being used, and don't need to rely on any host having it installed
beforehand.

## Deployment

To top it all off, I needed a way to package and host the web app. When using
Next.js, Vercel was the obvious option. Instead, I decided to cross-compile the
web server to linux and pacakge it into a container.

### Cross-Compilation

Rust includes support for cross-compilation, and adding the platforms I needed was as simple as adding these lines to my `WORKSPACE`:

```py
# /WORKSPACE

rust_register_toolchains(extra_target_triples = [
    "wasm32-unknown-unknown",
    "x86_64-unknown-linux-gnu",
    "aarch64-unknown-linux-gnu",
])
```

`rules_rust` uses the same bazel toolchain abstraction I mentioned in the
`wasm-opt` section of this post, and the cross-compilation really Just Works.

However, many rust crates also use C or C++, and I needed a toolchain that could
cross-compile those languages. When looking for the easiest cross-compilation
toolchain to use, I stumbled upon the blog post ["Zig Makes Go Cross Compilation Just
Work"](https://dev.to/kristoff/zig-makes-go-cross-compilation-just-work-29ho)
and a follow-on ["Zig Makes Rust Cross Compilation Just
Work"](https://actually.fyi/posts/zig-makes-rust-cross-compilation-just-work/).
These posts showcase using `zig` as a drop-in replacement for a C compiler and
linker with great success.

Luckily, the [`bazel-zig-cc`](https://git.sr.ht/~motiejus/bazel-zig-cc) project
provides bazel toolchain abstractions for the `zig` C/C++ toolchain. After
recommending [a small
fix](https://git.sr.ht/~motiejus/bazel-zig-cc/commit/79674a1d966b5c196a6f729ac7423223dc05cf83)
to address how `cargo` invokes `zig` as a linker, all I had to do was register
the linux `zig` toolchains in my `WORKSPACE`.

One minor issue I ran into was that `cargo` would have issues when using a
cross-compilation toolchain and compiling for the host architecture. To address
this, I wrote a
[wrapper](https://github.com/kcking/implfuture.dev/blob/490e84277c365cbdc332ffc15219308a809569d9/zig/zig.bzl)
around zig toolchain registration that omits the zig toolchain for the host
architecture.

### Container Image

With my cross-compiled server binary ready to go, the next step was packaging it
into a container. [`rules_docker`](https://github.com/bazelbuild/rules_docker)
provides bazel rules to create and manage OCI containers.

I first used the built-in `pkg_tar` rule to bundle all of the relevant files into one archive:

```py
# /server/BUILD

pkg_tar(
    name = "opt_tar",
    # :opt refers to optimized server artifacts that have passed through
    # wasm-opt and brotli
    srcs = [":opt"],
    include_runfiles = True,
    package_dir = "/app",
    # keeps relative path consistent for static files in both dev and release
    strip_prefix = "/server",
)
```

Then a single `container_image` rule packages everything into a container:

```py
# /server/BUILD

container_image(
    name = "image-amd64",
    architecture = "amd64",
    # References google-maintained base image defined in /WORKSPACE
    base = "@cc_base//image",
    cmd = ["/app/opt"],
    env = {
        # configure binary for production
        "HTTP_LISTEN_ADDR": "0.0.0.0:8080",
    },
    tars = [":opt_tar"],
    workdir = "/app",
)
```

> Note: `container_image` uses a [custom go
> library](https://github.com/bazelbuild/rules_docker/blob/master/container/go/pkg/oci/image.go)
> to build the image instead of requiring a Docker cli/daemon, however this tool
> doesn't currently work on Windows.

### Serverless

Infrastructure-wise, all I really needed was custom domain support, TLS
termination, and some execution environment. The first thing that came to mind
was something like AWS ECS (Elastic Container Service), but keeping a container
running all the time for just a personal website felt wasteful.

Instead, I settled on AWS Lambda, with a TLS certificate stored in ACM, and API
Gateway as a frontend. This setup provided unlimited auto-scaling, and didn't
even require setting up my own load balancer (load balancers are usually the
most expensive part of deploying a small project).

All I needed was for my web server to support lambda instead of expecting
incoming TCP traffic. Luckily, there's an awesome
[`lambda-web`](https://github.com/hanabu/lambda-web) crate that adapts common
rust web servers like Axum to the lambda runtime API. All I had to add to my web server was:

```rust
// /server/src/main.rs

if lambda_web::is_running_on_lambda() {
    lambda_web::run_hyper_on_lambda(route_service)
        .await
        .map_err(...)?;
} else {
    //  tcp listen code...
}
```

With `lambda-web`, the same axum handlers, including static file serving and
SSR, work for both my local development server _and_ the production lambda
runtime.

# Wrapping Up

Thank you for joining me on this rust + bazel journey! I was able to push
full-stack rust much further than I thought I would be able to. I converged on a
relatively convenient developer and deployment experience, building on the
shoulders of giants like bazel, rules_rust, and zig.

Don't get me wrong, there's still quite a few improvements I would like to see
in this project / the rust web ecosystem in general. To name a few:

- code splitting (right now the entire application wasm is loaded no matter which page the user is on)
- hot-reloading of app wasm so I can see updates without refreshing the page
- bazel remote build cache to speed up [github
  worker](https://github.com/kcking/implfuture.dev/actions) builds (currently
  takes 20-30m on a free worker)
- blog table of contents (could likely be implemented in a component that scans
  the blog DOM after first render)
- compiling the server to `wasi` architecture

I hope you learned a bit about rust, wasm, or bazel along the way! See you next
post!
